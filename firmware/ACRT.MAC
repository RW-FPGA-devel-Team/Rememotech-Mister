.Z80
ASEG
;
; Special version for REMEMOTECH or REMEMOrizer
; Modified Mar 2012, A.Key
; REMEMOTECH has larger screen buffer (4096 words instead of 2048)
; Relocated it from 0F400H to 0F2C0H.
; VDEB.COM accesses some variables and entrypoints within this code.
; Had to do some dirty embedded ORG/.PHASE to leave them in the right places.
;
	;	**********************
	;	*                    *
	;	* CRT OUTPUT HANDLER *
	;	*                    *
	;	**********************

DISP	EQU	20H

ADRLO	EQU	10H+DISP
ADRHI	EQU	11H+DISP
ASCD	EQU	12H+DISP
ATRD	EQU	13H+DISP

CRTCA	EQU	18H+DISP
CRTCD	EQU	19H+DISP

XMAX	EQU	80

ESC	EQU	27

	ORG	100H+0F2C0H-0E000H
;
.PHASE 0F2C0H

ACRT::		; ROM SIZING

CRTOUT::
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	HL,EXIT
	PUSH	HL	; SETUP RETURN ADDRESS

	DB	0C3H	; JMP OP-CODE
FRIG:	DW	INITL

CTAB:	DW	DUMMY	; ^@
	DW	DOTDO	; ^A
	DW	VCTDO	; ^B
	DW	CXYDO	; ^C
	DW	BKGSET	; ^D
	DW	EOLDO	; ^E
	DW	ATRSET	; ^F
	DW	BELDO	; ^G
	DW	BSDO	; ^H
	DW	TABDO	; ^I
	DW	LFDO	; ^J
	DW	UPDO	; ^K
	DW	CLRDO	; ^L
	DW	CRDO	; ^M
	DW	BLSET	; ^N
	DW	BLOFF	; ^O
	DW	COLSET	; ^P
	DW	COLSET	; ^Q
	DW	COLSET	; ^R
	DW	COLSET	; ^S
	DW	COLSET	; ^T
	DW	COLSET	; ^U
	DW	COLSET	; ^V
	DW	COLSET	; ^W
	DW	INITLZ	; ^X
	DW	FWDDO	; ^Y
	DW	HMEDO	; ^Z
	DW	ESCDO	; ^[
	DW	SCRSET	; ^\
	DW	PGESET	; ^]
	DW	CSSET	; ^^
	DW	CSOFF	; ^_

	;	NEXT 11 BYTES WILL BE USED FOR TEMP STORAGE

TEMP:
INITL:	PUSH	BC
	CALL	CLRDO
	POP	BC
	LD	HL,CRTGO
	LD	(FRIG),HL

	;	DETERMINE CHARACTER TYPE

CRTGO:	LD	A,C
	AND	11100000B
	JR	Z,CNTRL

	;	PRINTABLE CHARACTER

FRIG1:	CALL	DUMMY
	CALL	GETCUR
	LD	A,(PRATR)
	CALL	WRITE
	JP	FWDDO

	;	RESTORE REGESTERS

EXIT:	CALL	XYCALC
	CALL	SETCUR
	POP	HL
	POP	DE
	POP	BC
	POP	AF
	RET

	;	CONTROL CODE

CNTRL:	LD	HL,CTAB
	LD	D,0
	LD	A,C
	ADD	A,C
	LD	E,A
	ADD	HL,DE
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
	JP	(HL)

	;	CONTROL CODE HANDLERS

ESCDO:	CALL	FRIGIT
	LD	A,C
	CP	' '
	JR	C,NORMAL
	AND	11111B
	ADD	A,A
	LD	HL,ESCTAB
	LD	E,A
	LD	D,0
	ADD	HL,DE
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
	JP	(HL)

CXYDO:	CALL	FRIGIT
	LD	A,C
	SUB	32
	AND	7FH
	CP	XMAX
	JR	NC,CXSKIP
	LD	(XLOC),A
CXSKIP:	CALL	FRIGIT
	LD	A,C
	SUB	32
	AND	7FH
	LD	HL,(YMAX)	; L=(YMAX)
	CP	L
	JR	NC,CYSKIP
	LD	(YLOC),A
CYSKIP:

NORMAL:	LD	HL,CRTGO
	LD	(FRIG),HL
	RET

FRIGIT:	POP	HL
	LD	(FRIG),HL
	RET

BKGSET:	CALL	FRIGIT
	LD	A,C
	AND	111B
.8080
	RLC
	RLC
	RLC
.Z80
	LD	C,A
	LD	A,(PRATR)
	AND	11000111B
	OR	C
	LD	(PRATR),A
	LD	A,(NPATR)
	AND	11000111B
	OR	C
	LD	(NPATR),A
	JP	NORMAL

ATRSET:	CALL	FRIGIT
	LD	A,C
	LD	(PRATR),A
	LD	(NPATR),A
	JP	NORMAL

DOTDO:	CALL	FRIGIT
	LD	A,C
	SUB	32
	LD	(TEMP),A
	CALL	FRIGIT
	LD	A,C
	SUB	32
	LD	H,A	; H=Y
	LD	A,(TEMP)
	LD	L,A	; L=X
	CALL	PLOTD
	JP	NORMAL

VCTDO:	CALL	FRIGIT
	LD	A,C
	SUB	32
	LD	(TEMP+0),A
	CALL	FRIGIT
	LD	A,C
	SUB	32
	LD	(TEMP+1),A
	CALL	FRIGIT
	LD	A,C
	SUB	32
	LD	(TEMP+2),A
	CALL	FRIGIT
	LD	A,C
	SUB	32
	LD	C,A
	CALL	PLOTV
	JP	NORMAL

	;	HANDLERS NOT REQUIRING PERAMETERS

BLSET:	LD	A,(PRATR)
	OR	01000000B
	LD	(PRATR),A
	RET

BLOFF:	LD	A,(PRATR)
	AND	10111111B
	LD	(PRATR),A
	RET

CRDO:	XOR	A
	LD	(XLOC),A
DUMMY:	RET

PF40B::
	; may need to waste a few bytes
	; to get this next part in the right place
.DEPHASE
	ORG	100H+0F40BH-0E000H
.PHASE 0F40BH
	; VDEB.COM accesses (PRATR) and (SA)

	;	HANDLER STATUS STORAGE

ASCR:	DB	' '
ATRR:	DB	2
SCRFLG:	DB	1
PRATR::	DB	2
NPATR:	DB	2
WRMSK:	DB	11100000B	; WRITE TO ASC & ATR
SA::	DW	0
XLOC:	DB	0
YLOC:	DB	0
YMAX:	DB	24
YMAX2:	DW	XMAX*(24-2)

	;	HANDLERS NOT REQUIRING PERAMETERS

INITLZ:	LD	HL,SCRFLG
	LD	(HL),255
	INC	HL
	LD	(HL),2
	INC	HL
	LD	(HL),2
	INC	HL
	LD	(HL),11100000B
	CALL	CSSET
	CALL	CRDO
	CALL	LFDO
	JP	ESTD

CSSET:	LD	A,10
	OUT	(CRTCA),A
	LD	A,01100000B
	OUT	(CRTCD),A
	RET

CSOFF:	LD	A,10
	OUT	(CRTCA),A
	LD	A,00100000B
	OUT	(CRTCD),A
	RET

SCRSET:	LD	A,255
	LD	(SCRFLG),A
	RET

PGESET:	XOR	A
	LD	(SCRFLG),A
	RET

COLSET:	LD	A,C
	AND	111B
	LD	C,A
	LD	A,(PRATR)
	AND	11111000B
	OR	C
	LD	(PRATR),A
	RET


LFDO:	LD	A,(YLOC)
	LD	HL,(YMAX)	; L=(YMAX)
	DEC	L
	CP	L
	JR	Z,LFS
	INC	A
PAGEM:	LD	(YLOC),A
	RET
LFS:	LD	A,(SCRFLG)
	OR	A
	JR	Z,PAGEM
	CALL	SCRUP
	JP	ERLN

FWDDO:	LD	A,(XLOC)
	CP	XMAX-1
	JR	Z,FWDL
	INC	A
	LD	(XLOC),A
	RET
FWDL:	XOR	A
	LD	(XLOC),A
	JR	LFDO
BELDO:	IN	A,(ADRLO)
	RET

BSDO:	LD	A,(XLOC)
	OR	A
	JR	Z,BSU
	DEC	A
	LD	(XLOC),A
	RET
BSU:	LD	A,XMAX-1
	LD	(XLOC),A
	LD	A,(YLOC)
	OR	A
	JR	Z,BSS
	DEC	A
	LD	(YLOC),A
	RET
BSS:	XOR	A
	LD	(XLOC),A
	RET

TABDO:	LD	A,(XLOC)
	OR	111B
	LD	(XLOC),A
	JR	FWDDO
UPDO:	LD	A,(YLOC)
	OR	A
	JR	Z,UPS
	DEC	A
	LD	(YLOC),A
	RET
UPS:	RET

CLRDO:	CALL	CLRSCN
	LD	HL,0
	CALL	SETSA

HMEDO:	XOR	A
	LD	(XLOC),A
	LD	(YLOC),A
	RET

ERLN:	LD	A,(XLOC)
	PUSH	AF
	XOR	A
	LD	(XLOC),A
	CALL	EOLDO
	POP	AF
	LD	(XLOC),A
	RET

EOLDO:	CALL	XYCALC
	LD	A,(XLOC)
	LD	B,A
	JP	ERASE1

	;	UTILITIES

GRPMAP:	LD	A,C
	AND	7FH
	LD	C,A
	AND	01000000B
	RET	Z
	LD	A,C
	AND	00100000B
.8080
	RLC
	RLC
.Z80
	OR	C
	AND	10011111B
	LD	C,A
	RET

ALTMAP:	LD	A,C
	OR	10000000B
	LD	C,A
	RET

GETMSK:	LD	A,C
	CP	'0'
	JR	NZ,GETBIT
	XOR	A
	RET

GETBIT:	DEC	A
	AND	111B
	LD	C,A
	CALL	NCALC
	OR	A
	RET

GETCUR:	LD	A,14
	OUT	(CRTCA),A
	IN	A,(CRTCD)
	LD	H,A
	LD	A,15
	OUT	(CRTCA),A
	IN	A,(CRTCD)
	LD	L,A
	RET

XYCALC:	LD	A,(XLOC)
	LD	D,A
	LD	A,(YLOC)	; ASSUMES XMAX OF 80
	LD	E,A
CALC1:	PUSH	BC
	LD	A,E
	ADD	A,A
	ADD	A,A
	ADD	A,E		; y*5, y=0..120
	LD	L,A
	LD	H,0
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	B,H
	LD	C,L
	LD	HL,(SA)
	ADD	HL,BC
	LD	A,D
	POP	BC
CALC2:	LD	D,0
	LD	E,A
	ADD	HL,DE
	LD	A,H
	AND	00001111B	; REMEMOTECH has 4096 cells
				; real 80 column card has 2048, but coded for 16384
	LD	H,A
	RET

.8080
PLOT:	MOV	L,D
	MOV	H,E
PLOTD:	MOV	A,L
	STC
	CMC
	RAR
	MOV	D,A	; XA
	MVI	A,0
	RAL
	MOV	C,A
	MOV	A,H
	ANI	11B
	RLC
	ORA	C
	MOV	C,A	; N
	MOV	A,H
	RRC
	RRC
	ANI	00111111B
.Z80
	LD	HL,(YMAX)	; L=(YMAX)
	CP	L
.8080
	RNC
	MOV	E,A	; YA
	MOV	A,D
	CPI	XMAX
	RNC
	CALL	NCALC
	MOV	C,A
	CALL	CALC1
	MOV	A,H
	ANI	111B
	MOV	H,A
	OUT	ADRHI
	MOV	A,L
	OUT	ADRLO
	IN	ATRD
	MOV	E,A
	ANI	10000000B
	IN	ASCD
.Z80
	JR	NZ,ORWR
	XOR	A
ORWR:	LD	B,A
	LD	A,(NPATR)
	PUSH	AF
	AND	111B
	JR	Z,ERSD
	POP	AF
	OR	10000000B
	OUT	(ATRD),A
	LD	A,B
	OR	C
ORWR1:	OUT	(ASCD),A
	LD	A,(WRMSK)
	OR	H
	OUT	(ADRHI),A
	LD	A,L
	OUT	(ADRLO),A
	RET
ERSD:	POP	AF
	LD	A,E
	OR	10000000B
	OUT	(ATRD),A
	LD	A,C
.8080
	CMA
	ANA	B
.Z80
	JR	ORWR1
.8080

PLOTV:	LXI	H,0
	DAD	SP
	SHLD	TEMP+8
	LXI	SP,VSTK
STKSKP:	LXI	H,TEMP
	MOV	D,M
	INX	H
	MOV	E,M
	INX	H
	MOV	H,M
	MOV	L,C

VECT:	LXI	B,0
	PUSH	B
	MOV	A,D
	CMP	H
.Z80
	JR	C,NOXG
	JR	NZ,XG
	LD	A,E
	CP	L
	JR	C,NOXG
.8080
XG:	XCHG
NOXG:	MOV	B,H
	MOV	C,L
	LXI	H,INRAL
	MVI	M,03CH	; (INR A)
	MOV	A,E
	CMP	C
.Z80
	JR C,	LOOP
.8080
	MVI	M,00CH	; (INR C)
LOOP:	MOV	A,D
	CMP	B
.Z80
	JR Z,	EQX
.8080
	PUSH	B
	ADD	B
	RAR
	MOV	B,A
	INR	A
	MOV	H,A
	MOV	A,E
	CMP	C
.Z80
	JR Z,	EQY
.8080
NEQY:	ADD	C
	RAR
	MOV	C,A
INRAL:	INR	A
EQY:	MOV	L,A
	PUSH	H
.Z80
	JR	LOOP
.8080
EQX:	MOV	A,E
	CMP	C
.Z80
	JR Z,	EQXY
.8080
	PUSH	B
	MOV	H,D
.Z80
	JR	NEQY
.8080
EQXY:	CALL	PLOT
	POP	D
	MOV	A,D
	ORA	E
.Z80
	JR Z,	PLEXIT
.8080
	POP	B
.Z80
	JR	LOOP
.8080

PLEXIT:	LHLD	TEMP+8
	SPHL
	RET

NCALC:	INR	C
	MVI	A,1
NCALCL:	DCR	C
	RZ
	RLC
.Z80
	JR	NCALCL

SETCUR:	LD	A,14
	OUT	(CRTCA),A
	LD	A,H
	OUT	(CRTCD),A
	LD	A,15
	OUT	(CRTCA),A
	LD	A,L
	OUT	(CRTCD),A
	RET

SCRUP:	LD	HL,(SA)
	LD	A,XMAX
	CALL	CALC2

SETSA:	LD	A,12
	OUT	(CRTCA),A
	LD	A,H
	OUT	(CRTCD),A
	LD	A,13
	OUT	(CRTCA),A
	LD	A,L
	OUT	(CRTCD),A
	LD	(SA),HL
	RET

CLRSCN:	LD	HL,0
	LD	C,' '
CLRLP:	LD	A,(NPATR)
	CALL	WRITE
	INC	HL
	LD	A,H
	CP	10H		; 4096 cells
	JR	NZ,CLRLP
	RET

PF65D::
	; may need to waste a few bytes
	; to get this next part in the right place
.DEPHASE
	ORG	100H+0F65DH-0E000H
.PHASE 0F65DH
	; VDEB.COM calls here with something in the C register
	; Not sure what we are supposed to do with it
LF65D::
	RET

ERASE1:	LD	A,256-XMAX
	ADD	A,B
	LD	B,A	; SET UP BYTE COUNT
	LD	A,' '
	OUT	(ASCD),A
	LD	A,(NPATR)
	OUT	(ATRD),A
	LD	A,(WRMSK)
	LD	D,A
ERLP0:	LD	A,H
	AND	00001111B	; 4096 cells in REMEMOTECH
	OR	D
	OUT	(ADRHI),A
ERLP1:	LD	A,L
	OUT	(ADRLO),A
	INC	B
	RET	Z
	INC	L
	JR	NZ,ERLP1
	INC	H
	JR	ERLP0

WRITE:	OUT	(ATRD),A
	LD	A,C
	OUT	(ASCD),A
	LD	A,(WRMSK)
	LD	D,A
	LD	A,H
	AND	00001111B	; 4096 cells in REMEMOTECH
	OR	D
	OUT	(ADRHI),A
	LD	A,L
	OUT	(ADRLO),A
	RET

	;	ESCAPE SEQUENCE LOOK-UP TABLE

ESCTAB:	DW	NORMAL	; @
	DW	EALT	; A
	DW	EBOTH	; B
	DW	ESCRL	; C
	DW	EPGE	; D
	DW	ECSON	; E
	DW	ECSOFF	; F
	DW	EGRPH	; G
	DW	NORMAL	; H
	DW	EIBLLN	; I
	DW	EDCSLN	; J
	DW	NORMAL	; K
	DW	NORMAL	; L
	DW	NORMAL	; M
	DW	ENPATR	; N
	DW	NORMAL	; O
	DW	EPRATR	; P
	DW	NORMAL	; Q	; in SCPM ROM, disable color, cls
	DW	EREAD	; R	; in SCPM ROM, enable color, cls
	DW	ESTD	; S
	DW	ESIPR	; T
	DW	ESINP	; U
	DW	ESIBT	; V
	DW	EWRMS	; W
	DW	CNTSIM	; X
	DW	E24LIN	; Y	; in REMEMOTECH, go to 80x24 mode, cls
	DW	E48LIN	; Z	; in REMEMOTECH, go to 80x48 mode, cls
	DW	NORMAL	; [
	DW	NORMAL	; \
	DW	NORMAL	; ]
	DW	NORMAL	; ^
	DW	NORMAL	; _

	;	ESCAPE SEQUENCE HANDLERS

ESTD:	LD	HL,DUMMY
	LD	(FRIG1+1),HL
	JP	NORMAL

EALT:	LD	HL,ALTMAP
	LD	(FRIG1+1),HL
	JP	NORMAL

EGRPH:	LD	HL,GRPMAP
	LD	(FRIG1+1),HL
	JP	NORMAL

CNTSIM:	CALL	FRIGIT
	LD	A,C
	AND	11111B
	LD	C,A
	CALL	NORMAL
	JP	CNTRL

ESCRL:	CALL	SCRSET
	JP	NORMAL
EPGE:	CALL	PGESET
	JP	NORMAL
ECSON:	CALL	CSSET
	JP	NORMAL
ECSOFF:	CALL	CSOFF
	JP	NORMAL

ESIPR:	CALL	FRIGIT
	LD	A,C
	LD	(PRATR),A
	JP	NORMAL

ESINP:	CALL	FRIGIT
	LD	A,C
	LD	(NPATR),A
	JP	NORMAL

ESIBT:	CALL	FRIGIT
	LD	A,C
	LD	(NPATR),A
	LD	(PRATR),A
	JP	NORMAL

EDCSLN:	LD	A,(XLOC)
	PUSH	AF
	XOR	A
	LD	(XLOC),A
	CALL	XYCALC
	EX	DE,HL
	LD	HL,XMAX
	ADD	HL,DE
	LD	A,(YLOC)
	LD	B,A
SHFTUP:	LD	A,B
	PUSH	DE
	LD	DE,(YMAX)	; E=(YMAX)
	DEC	E
	CP	E
	POP	DE
	JR	Z,SHUPOK
	INC	B
	CALL	TRXMAX
	JR	SHFTUP
SHUPOK:	EX	DE,HL
	LD	B,0
	CALL	ERASE1
	POP	AF
	LD	(XLOC),A
	JP	NORMAL

EIBLLN:	LD	HL,(SA)
	LD	DE,(YMAX2)	; DE=XMAX*(YMAX-2)
	ADD	HL,DE
	EX	DE,HL
	LD	HL,XMAX
	ADD	HL,DE
	EX	DE,HL
	LD	BC,(YMAX-1)	; B=(YMAX)
	DEC	B		; B=(YMAX)-1
	LD	A,(YLOC)
	LD	C,A
SHFTDN:	LD	A,B
	CP	C
	JR	Z,SHDNOK
	DEC	B
	CALL	TRXMAX
	PUSH	BC
	LD	BC,-2*XMAX
	ADD	HL,BC
	EX	DE,HL
	ADD	HL,BC
	EX	DE,HL
	POP	BC
	JR	SHFTDN

SHDNOK:	EX	DE,HL
	LD	B,0
	CALL	ERASE1
	JP	NORMAL

TRXMAX:	PUSH	BC
	LD	A,(WRMSK)
	LD	B,A
	LD	C,XMAX
TRLP:	LD	A,H
	AND	1111B		; 4096 cells in REMEMOTECH
	OUT	(ADRHI),A
	LD	A,L
	OUT	(ADRLO),A
	IN	A,(ASCD)
	OUT	(ASCD),A
	IN	A,(ATRD)
	OUT	(ATRD),A
	LD	A,D
	AND	1111B		; 4096 cells in REMEMOTECH
	OR	B
	OUT	(ADRHI),A
	LD	A,E
	OUT	(ADRLO),A
	INC	DE
	INC	HL
	DEC	C
	JR	NZ,TRLP
	POP	BC
	RET

PF7C4::
	; may need to waste a few bytes
	; to get this next part in the right place
.DEPHASE
	ORG	100H+0F7C4H-0E000H
.PHASE 0F7C4H
	; VDEB.COM calls here with (PRATR) in the A register
	; Not sure what we are supposed to do with it
LF7C4::
	RET
	
EREAD:	CALL	XYCALC
	LD	A,H
	OUT	(ADRHI),A
	LD	A,L
	OUT	(ADRLO),A
	IN	A,(ASCD)
	LD	(ASCR),A
	IN	A,(ATRD)
	LD	(ATRR),A
	JP	NORMAL

EWRMS:	CALL	FRIGIT
	LD	A,C
	LD	C,11100000B
	CP	'0'
	JR	Z,SWRM
	LD	C,11000000B
	CP	'1'
	JR	Z,SWRM
	LD	C,10100000B
	CP	'2'
	JP	NZ,NORMAL
SWRM:	LD	A,C
	LD	(WRMSK),A
	JP	NORMAL

EPRATR:	CALL	FRIGIT
	CALL	GETMSK
	JR	Z,SETPR
	LD	C,A
	LD	A,(PRATR)
	OR	C
SETPR:	LD	(PRATR),A
	JP	NORMAL

ENPATR:	CALL	FRIGIT
	CALL	GETMSK
	JR	Z,SETNP
	LD	C,A
ENPA1:	LD	A,(NPATR)
	OR	C
SETNP:	LD	(NPATR),A
	JP	NORMAL

EBOTH:	CALL	FRIGIT
	CALL	GETMSK
	JR NZ,	SETB
	LD	(PRATR),A
	JR	SETNP
SETB:	LD	C,A
	LD	A,(PRATR)
	OR	C
	LD	(PRATR),A
	JR	ENPA1

; REMEMOTECH special feature, 80x24 and 80x48 modes

E24LIN:	LD	A,24
	LD	HL,XMAX*(24-2)
	LD	C,0
	JR	EXXLIN

E48LIN:	LD	A,48
	LD	HL,XMAX*(48-2)
	LD	C,1

EXXLIN:	LD	(YMAX),A
	LD	(YMAX2),HL
	PUSH	BC
	CALL	CLRDO
	POP	BC
	LD	A,31		; REMEMOTECH special 80 column register
	OUT	(CRTCA),A
	LD	A,C
	OUT	(CRTCD),A
	JP	NORMAL


	;	END OF ESCAPE SEQUENCE HANDLERS

	DS	16*2
VSTK:

EACRT::		; ROM SIZING

;
.DEPHASE
;
;
;
END
